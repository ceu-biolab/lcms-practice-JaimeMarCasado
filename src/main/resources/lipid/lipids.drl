package lipid;

unit LipidScoreUnit;


rule "Score +1 if same type, same DB, more C => higher RT"
when
    $a1: /annotations[
        $rt1: rtMin,
        $lipid1: lipid,
        $c1: lipid.getCarbonCount(),
        $db: lipid.getDoubleBondsCount(),
        $type: lipid.getLipidType()
    ]
    $a2: /annotations[
        this != $a1,
        lipid.getLipidType() == $type,
        lipid.getDoubleBondsCount() == $db,
        lipid.getCarbonCount() > $c1,
        rtMin > $rt1
    ]
then
    $a1.addScore(1);
    $a2.addScore(1);
end

rule "Score +1 if same type, same C, more DB => lower RT"
when
    $a1: /annotations[
        $rt1: rtMin,
        $lipid1: lipid,
        $c: lipid.getCarbonCount(),
        $db1: lipid.getDoubleBondsCount(),
        $type: lipid.getLipidType()
    ]
    $a2: /annotations[
        this != $a1,
        lipid.getLipidType() == $type,
        lipid.getCarbonCount() == $c,
        lipid.getDoubleBondsCount() > $db1,
        rtMin < $rt1
    ]
then
    $a1.addScore(1);
    $a2.addScore(1);

end

rule "Score +1 if lipid type follows correct elution order"
when
    $a1: /annotations[
        $rt1: rtMin,
        $t1: lipid.getLipidType()
    ]
    $a2: /annotations[
        this != $a1,
        $t2: lipid.getLipidType(),
        lipid.getLipidType().ordinal() > $t1.ordinal(),
        rtMin > $rt1
    ]
then
    $a1.addScore(1);
    $a2.addScore(1);
end

rule "Score -1 if same type, same DB, more C => lower RT"
when
    $a1: /annotations[
        $rt1: rtMin,
        $c1: lipid.getCarbonCount(),
        $db: lipid.getDoubleBondsCount(),
        $type: lipid.getLipidType()
    ]
    $a2: /annotations[
        this != $a1,
        lipid.getLipidType() == $type,
        lipid.getDoubleBondsCount() == $db,
        lipid.getCarbonCount() > $c1,
        rtMin < $rt1
    ]
then
    $a1.addScore(-1);
    $a2.addScore(-1);
end

rule "Score -1 if same type, same C, more DB => higher RT"
when
    $a1: /annotations[
        $rt1: rtMin,
        $c: lipid.getCarbonCount(),
        $db1: lipid.getDoubleBondsCount(),
        $type: lipid.getLipidType()
    ]
    $a2: /annotations[
        this != $a1,
        lipid.getLipidType() == $type,
        lipid.getCarbonCount() == $c,
        lipid.getDoubleBondsCount() > $db1,
        rtMin > $rt1
    ]
then
    $a1.addScore(-1);
    $a2.addScore(-1);
end

rule "Score -1 if lipid type elutes in wrong order"
when
    $a1: /annotations[
        $rt1: rtMin,
        $t1: lipid.getLipidType()
    ]
    $a2: /annotations[
        this != $a1,
        $t2: lipid.getLipidType(),
        lipid.getLipidType().ordinal() > $t1.ordinal(),
        rtMin < $rt1
    ]
then
    $a1.addScore(-1);
    $a2.addScore(-1);
end